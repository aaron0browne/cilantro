---
layout: page
title: "Patterns"
category: ref
date: 2013-06-06 16:36:26
---

## Module Headers

Module dependencies should be defined one per line rather than a single line. This is for consistency since most modules have many dependencies and for readability.

This:

```coffeescript
define [
    'jquery'
    'underscore'
    'marionette'
], ($, _, Marionette) ->
    # ...
```

Not this:

```coffeescript
define ['jquery', 'underscore', 'marionette'], ($, _, Marionette) ->
    # ...
```

## HTML Templates

Cilantro uses HTML templates for defining the content of a UI view. HTML template files are defined in `src/templates/` and should follow the name module path for the view the template is associated with. If it is a general purpose template, name it something informative.

When templates are used in a module, list them at the end of the dependency array and use the CoffeeScript [splat syntax](http://coffeescript.org/#splats) named `templates...`. The simple convention is to convert the array of compiled templates into an object using Underscore's [`_.object` function](http://underscorejs.org/#object). Even for a single template, this convention should be followed for clarity.

```coffeescript
define [
    'underscore'
    'marionette'
    'tpl!templates/views/foo.html'
    'tpl!templates/views/bar.html'
], (_, Marionette, templates...) ->

    # Uses Underscore object function zip together the keys and compiled
    # templates to produce a templates object.
    templates = _.object ['foo', 'bar'], templates

    class Foo extends Marionette.ItemView
        template: templates.foo

    class Bar extends Marionette.ItemView
        template: templates.bar

    { Foo, Bar }
```

## View Data

Most views in Cilantro have a primary model or collection that is being used as the data source for rendering the view. [Backbone](http://backbonejs.org/#View-constructor) and [Marionette](https://github.com/marionettejs/backbone.marionette/blob/master/docs/marionette.itemview.md#modelevents-and-collectionevents) have facilities for automatically referencing and binding to the passed in model or collection. Some views require supplementary data sources or simply act as proxies for passing references down to subviews. The convention is to define a `data` attribute for storing the supplementary models and collections for the view. If the data is required, an error should be thrown if the data is not present.

```coffeescript
class SomeLayout extends Marionette.Layout
    initialize: ->
        @data = {}
        if not (@data.context = @options.context)
            throw new Error 'context required'
        if not (@data.view = @options.view)
            throw new Error 'view required'
        if not (@data.concepts = @options.concepts)
            throw new Error 'concepts required'

    ...
```

## Layouts

Marionette defines a [Layout](https://github.com/marionettejs/backbone.marionette/blob/master/docs/marionette.layout.md#marionettelayout) class that manages rendering regions (other views) within the layout. Marionette requires the region selectors be defined in the `regions` object property of the class definition. For symmetry and extensibility, the corresponding view classes that will be rendered in those regions should be defined on the `regionViews` property using the same keys.

```coffeescript
class SomeLayout extends Marionette.Layout
    regions:
        region1: '.region1'
        region2: '.region2'

    regionViews:
        region1: SomeView1
        region2: SomeView2

    onRender: ->
        @region1.show new @regionViews.region1
            model: @model

        @region2.show new @regionViews.region2
            model: @model
```

## Loading, Empty, and Error Views

All of Cilantro's user interface components are load asynchronously primarily because everything is data driven by the Serrano-compatible endpoint.

Unfortunately this can have ramifications on the user experience since there is a split second (hopefully no longer) that there is nothing on the page as the components are loading. In certain cases views can be rendered immediately and populated once the data is fetched.

To alleviate this, a [Marionette Region](https://github.com/marionettejs/backbone.marionette/blob/master/docs/marionette.region.md#basic-use) can be used to show a _loading_ view until the data is ready to show the primary view. Cilantro makes heavy use of [Layouts](https://github.com/marionettejs/backbone.marionette/blob/master/docs/marionette.layout.md#basic-usage) for defining and manipulating regions, so integrating this behavior is painless.
