---
layout: page
title: "Patterns"
category: ref
date: 2013-06-06 16:36:26
---

## HTML Templates

Since there is no HTML page to embed templates into, templates must be defined either within a module (messy) or in external files and dynamically loaded (not messy).

Simply create (or resuse) a HTML file in `templates/` and use the RequireJS `tpl` plugin to load the compiled template.

```coffeescript
define [
    '../../core'
    'tpl!templates/views/foo.html'
], (c, compiledFoo) ->

    class Foo extends c.Marionette.ItemView
        template: compiledFoo

    { Foo }
```

### Multiple Templates

A pattern when multiple templates need to be included is using the CS splat syntax and wrapping the templates in an object using Underscore's [`_.object` function](http://underscorejs.org/#object). Even for a single template, this is the preferred pattern for clarity.

```coffeescript
define [
    '../../core'
    'tpl!templates/views/foo.html'
    'tpl!templates/views/bar.html'
    'tpl!templates/views/baz.html'
    'tpl!templates/views/qux.html'
], (c, templates...) ->

    # Uses Underscore object function zip together the keys and compiled
    # templates and produces an object.
    templates = c._.object ['foo', 'bar', 'baz', 'qux'], templates

    class Foo extends c.Marionette.ItemView
        template: templates.foo

    class Bar extends c.Marionette.ItemView
        template: templates.bar

    ...
```

## Layouts

When defining a layout subclass, a convention to make the layout more extensible is defining a `regionViews` object that maps the region name to the default view class:

```coffeescript
class SomeLayout extends c.Marionette.Layout
    regions:
        region1: '.region1'
        region2: '.region2'
        ...

    # Map corresponding view class to region. This makes it
    # easier to extend. This can also be a function that returns
    # an object.
    regionViews:
        region1: SomeView1
        region2: SomeView2
        ...

    # Define an options object or function that returns an object
    # to pass into the region on initialization.
    regionViewOptions:
        region1: ->
        region2: ->
        ...

    # Iterate over each region view, instantiate a view with options
    # (most likely derived from this layout), and show it in the region.
    onRender: ->
        regionViews = c._.result(@, 'regionViews')
        regionViewOptions = c._.result(@, 'regionViewOptions')

        for key, klass of regionViews
            options = c._.result(regionViewOptions, key)
            view = new klass(options)
            @[key].show view
```

## "Loading" Views

All of Cilantro's user interface components are load asynchronously primarily because everything is data driven by the Serrano-compatible endpoint.

Unfortunately this can have ramifications on the user experience since there is a split second (hopefully no longer) that there is nothing on the page as the components are loading. In certain cases views can be rendered immediately and populated once the data is fetched.

To alleviate this, a [Marionette Region](https://github.com/marionettejs/backbone.marionette/blob/master/docs/marionette.region.md#basic-use) can be used to show a _loading_ view until the data is ready to show the primary view. Cilantro makes heavy use of [Layouts](https://github.com/marionettejs/backbone.marionette/blob/master/docs/marionette.layout.md#basic-usage) for defining and manipulating regions, so integrating this behavior is painless.

Here is a snippet from the `QueryWorkflow` layout making use of this pattern:

```coffeescript
class QueryWorkflow extends c.Marionette.Layout
    className: 'query-workflow'

    template: templates.query

    regions:
        concepts: '.concepts-region'
        workspace: '.workspace-region'
        context: '.context-region'

    onRender: ->
        # Render primary view immediately since it does not depend on
        # any data.
        @workspace.show new concept.ConceptWorkspace

        # Show loading views for data-dependent views initially
        @concepts.show new base.LoadView
        @context.show new base.LoadView

        # When the data is ready, show the target views
        c.data.concepts.ready =>
            @concepts.show new concept.ConceptPanel
                collection: c.data.concepts.queryable

        c.data.contexts.ready =>
            @context.show new context.Context
                model: c.data.contexts.getSession()

```
